TITLE	RACING GAME

	.Z80
	ASEG

WRTVRM	EQU	004DH
FILVRM	EQU	0056H
LDIRVM	EQU	005CH
WRTPSG	EQU	0093H
GTSTCK	EQU	00D5H
GTTRIG	EQU	00D8H
BREAKX	EQU	00B7H
SNSMAT	EQU	0141H
ENASLT	EQU	0024H
BEEP	EQU	00C0H

H.TIMI	EQU	0FD9FH
EXPTBL	EQU	0FCC1H
RAMAD1	EQU	0F342H

CSDATA	EQU	05000H
;COSDT	EQU	0D000H

ROAD	EQU	80H
EN_W	EQU	88H
EN_R	EQU	90H
CT_L	EQU	8DH
CT_R	EQU	8EH
CT_W	EQU	8FH
CT_K	EQU	0A7H
KUSA	EQU	0A0H
SUNA	EQU	0A8H
SIKIRI	EQU	0B0H
TR	EQU	0F0H
WAT	EQU	78H	
;---SPRITE---
SPDRIF	EQU	15
SPMAPM	EQU	8
SPMAPR	EQU	11
;---BALANCE---
VHAN	EQU	2
VDRHAN	EQU	3
VDRIF	EQU	6
VSPIN	EQU	110
SPINR	EQU	15	;SPIN KAKUDO
SPINM	EQU	6	;SPIN GENSOKU
VBREAK	EQU	15
IDOL	EQU	1
KUSASP	EQU	150	;KUSA SPEED
SUNASP	EQU	150	;SUNA SPEED
WATRED	EQU	8	;SPEED 5 REDUSE
WDROP	EQU	150	;OTIRU SOKUDO
DRSPD	EQU	150	;DRIFT SPEED
WATSPD	EQU	50	
RBV	EQU	7	;RIBAL SUU
GWT	EQU	2

INITSC	EQU	0A800H

	ORG	09200H

START:
	JP	NOR
	JP	INITSC
NOR:	CALL	INTON
	CALL	SLTCHN
	CALL	MAIN
	CALL	REV	;int off
	RET

MAIN:
	CALL	INITMAP
;;;;;;;;;TEST	
;	LD	HL,0A000H
;	LD	DE,5000H
;	LD	BC,2400H
;	LDIR
;	LD	HL,WORKAREA
;	LD	(HL),255
;	INC	HL
;	LD	(HL),1
;	INC	HL
;	LD	(HL),0
;	INC	HL
;	LD	(HL),10	;X
;	INC	HL
;	LD	(HL),50	;Y
;	LD	A,0
;	LD	(CONDR),A
;;;;;;;;;;;;;;;;;;;;;;;;;;;;

MALOOP:	CALL	SELECTCS	;RETURN A<-COURSE NO.
	CALL	INIPARA2
	CALL	MKCSDAT
	CALL	INITPARA
	CALL	MKSCR
	CALL	PRE
	LD	A,1
	LD	(SOUND),A
	CALL	TIMER
	LD	A,1
	CALL	LTIMER
	CALL	RACING
	JP	MALOOP

;###########SUB	ROUTINE###########

MKCSDAT:
	LD	A,(SLCOURSE)
	DEC	A
	LD	B,A
	SLA	A
	SLA	A
	ADD	A,B
	ADD	A,3
	LD	E,A
	LD	D,0
	LD	HL,CSPARA
	ADD	HL,DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	INC	HL
	SRL	H
	RR	L
	
	LD	DE,05000H
	LD	B,96
	LD	A,0B0H
MKCDL1:	LD	(DE),A
	INC	DE
	DJNZ	MKCDL1
	
	PUSH	HL
	LD	A,(SLCOURSE)
	DEC	A
	SLA	A
	LD	HL,CSDATAD
	ADD	A,L
	LD	L,A
	JP	NC,NOINC
	INC	H
NOINC:	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	LD	H,B
	LD	L,C
	POP	BC
TENL:	PUSH	BC
	LD	A,(HL)
	INC	HL
	LD	B,(HL)
	INC	HL
TENINL:	LD	(DE),A
	INC	DE
	DJNZ	TENINL
	POP	BC
	DEC	BC
	LD	A,B
	OR	C
	JP	NZ,TENL

	LD	B,96
	LD	A,0B0H
MKCDL2:	LD	(DE),A
	INC	DE
	DJNZ	MKCDL2
	CALL	BEEP;;;;;
	RET

CSDATAD:
	DW	0B000H,0B5E0H,0BDE0H,0C760H,0CD00H
SELECTCS:
	CALL	CLS2
	CALL	MAP
	XOR	A
	CALL	PRMAP
	CALL	MKSCR_SUB
	LD	HL,SELCSM1
	LD	DE,1800H+32*3+5
	LD	BC,13
	CALL	LDIRVM
	LD	A,1
SCSLP:	PUSH	AF
	LD	B,A
	ADD	A,'0'
	LD	(SELCSM2+4),A
	LD	HL,1800H+10
	LD	A,B
	DEC	A
	SLA	A
	ADD	A,7
	LD	E,A
	LD	D,0
	SLA	E
	RL	D
	SLA	E
	RL	D
	SLA	E
	RL	D
	SLA	E
	RL	D
	SLA	E
	RL	D
	ADD	HL,DE
	EX	DE,HL
	LD	HL,SELCSM2
	LD	BC,5
	CALL	LDIRVM
	POP	AF
	INC	A
	CP	6
	JP	C,SCSLP
	LD	A,1
	LD	(SLCOURSE),A
CLP:	CALL	SLPR
	XOR	A
	CALL	GTTRIG
	PUSH	AF
	LD	A,1
	CALL	GTTRIG
	LD	B,A
	POP	AF
	OR	B
	JP	NZ,SLEXIT
	XOR	A
	CALL	GTSTCK
	PUSH	AF
	LD	A,1
	CALL	GTSTCK
	LD	B,A
	POP	AF
	OR	B
	CP	1
	JP	Z,UPW
	CP	5
	JP	Z,DWW
	JP	CLP
UPW:	LD	A,(SLCOURSE)
	DEC	A
	JP	Z,CLP
	LD	(SLCOURSE),A
	LD	A,10
	CALL	WAIT
	JP	CLP
DWW:	LD	A,(SLCOURSE)
	INC	A
	CP	6
	JP	Z,CLP
	LD	(SLCOURSE),A
	LD	A,10
	CALL	WAIT
	JP	CLP

SLEXIT:	LD	HL,1B08H
	LD	A,209
	CALL	WRTVRM
	RET

SLPR:	LD	A,(SLCOURSE)
	CALL	PRMAP
	LD	A,(SLCOURSE)
	DEC	A
	SLA	A
	ADD	A,7
	SLA	A
	SLA	A
	SLA	A
	LD	HL,1B08H
	CALL	WRTVRM
	INC	HL
	LD	A,64
	CALL	WRTVRM
	INC	HL
	LD	A,35*4
	CALL	WRTVRM
	INC	HL
	LD	A,15
	CALL	WRTVRM
	RET

SELCSM1:
	DB	'COURSE',2FH,'SELECT'
SELCSM2:
	DB	'No.',2FH,0	
INIPARA2:
	LD	A,(SLCOURSE)
	LD	HL,CSPARA
	DEC	A
	LD	B,A
	SLA	A
	SLA	A
	ADD	A,B
	LD	E,A
	LD	D,0
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	LD	(MX),A
	LD	A,(HL)
	INC	HL
	LD	(MY),A
	LD	A,(HL)
	INC	HL
	LD	(CONDR),A
	RET

INITPARA:
	XOR	A
	LD	(ELLIGAL),A
	LD	(SOUND),A
	LD	(MSPD),A
	LD	(GOALF),A
	DEC	A
	LD	(MLAP),A
	LD	(MAXSPD),A
	LD	A,5
	LD	(LAP),A
	LD	A,(CONDR)
	RRCA
	RRCA
	LD	(MH),A
	LD	(MDR),A
	RET

SLTCHN:
	LD	A,(RAMAD1)
	LD	HL,4000H
	CALL	ENASLT
	RET	
INITMAP:
	LD	HL,4000H
	LD	DE,4001H
	LD	BC,3FFEH
	LD	(HL),0A0H
	LDIR
	RET

RACING:
	LD	A,GWT
	LD	(POSCNT),A
	CALL	BREAKX
	EI
	JP 	NC,RCNEXT
	POP	AF
	POP	AF
	JP	REV
RCNEXT:	CALL	PRMINUTE	;TIME
	XOR	A
	CALL	GTSTCK
	LD	B,A
	PUSH	BC
	LD	A,1
	CALL	GTSTCK
	POP	BC
	OR	B
	EX	AF,AF'
	LD	A,(SPIN)
	OR	A
	JP	NZ,RCSPIN
	LD	A,(DRIF)
	OR	A
	JP	NZ,RCDRIF
;----
RCNORM:	EX	AF,AF'
	CP	2
	JP	C,RCACSEL
	CP	4
	JP	C,RCRTURN
	JP	Z,RCRDRIF
	CP	6
	JP	C,RCACSEL
	JP	Z,RCLDRIF
	JP	RCLTURN
RCRTURN:
	LD	HL,MH
	LD	B,(HL)
	LD	A,(DMH)
	ADD	A,B
	LD	(HL),A
	INC	HL
	LD	(HL),A
	JP	RCACSEL
RCLTURN:
	LD	HL,MH
	LD	B,(HL)
	LD	A,(DMH)
	NEG
	ADD	A,B
	LD	(HL),A
	INC	HL
	LD	(HL),A
	JP	RCACSEL
RCRDRIF:
	LD	A,(MSPD)
	CP	DRSPD
	JP	C,RCRTURN
	LD	HL,MH
	LD	B,(HL)
	LD	A,VDRHAN
	NOP
	ADD	A,B
	LD	(HL),A
	INC	HL
	LD	A,(DMDF)
	ADD	A,B
	LD	(HL),A
	INC	HL	;DRIF
	LD	(HL),1
	XOR	A
	LD	(DRIFDR),A
	JP	RCBREAK
RCLDRIF:
	LD	A,(MSPD)
	CP	DRSPD
	JP	C,RCLTURN
	LD	HL,MH
	LD	B,(HL)
	LD	A,VDRHAN
	NOP
	NEG
	ADD	A,B
	LD	(HL),A
	INC	HL
	LD	A,(DMDF)
	NEG
	ADD	A,B
	LD	(HL),A
	INC	HL	;DRIF
	LD	A,1
	LD	(HL),A
	LD	(DRIFDR),A
	JP	RCBREAK
;---
RCDRIF:
	EX	AF,AF'
	CP	2
	JP	C,DRNORM
	CP	5
	JP	C,DRR
	JP	Z,DRNORM
DRL:	LD	A,(DRIFDR)
	OR	A
	JP	Z,REVH
	JP	NORH
DRR:	LD	A,(DRIFDR)
	OR	A
	JP	Z,NORH
	JP	REVH

DRNORM:	LD	HL,MH
	LD	B,(HL)
	LD	A,(DRIFDR)
	LD	C,A
	LD	A,VDRHAN
	NOP
	INC	C
	DEC	C
	JP	Z,DML1
	NEG
DML1:	ADD	A,B
	LD	(HL),A
	INC	HL	;MDR
	SUB	(HL)	;MH-MDR
	CP	2
	JP	C,DROFF
	CP	-3
	JP	NC,DROFF
	JP	RCBREAK

DROFF:	LD	A,(HL)
	LD	(MH),A
	INC	HL
	LD	(HL),0
	JP	RCACSEL

NORH:	LD	HL,MH
	LD	A,(DRIFDR)
	LD	E,A
	LD	A,VDRHAN
	NOP
	INC	E
	DEC	E
	JP	Z,NHL1
	NEG
NHL1:	LD	B,(HL)
	ADD	A,B
	LD	(HL),A
	LD	D,A

	INC	HL	;MDR
	LD	A,(DMDF)
	INC	E
	DEC	E
	JP	Z,NHL2
	NEG
NHL2:	LD	B,(HL)
	ADD	A,B
	LD	(HL),A
	SUB	D	;MDR-MH
	OR	A
	JP	P,NHL3
	NEG
NHL3:	LD	HL,DSPIN
	CP	(HL)
	JP	C,RCBREAK
	LD	A,1
	LD	(SPIN),A
	JP	RCBREAK

REVH:	XOR	A
	LD	(DRIF),A
	LD	A,(MDR)
	LD	(MH),A
	JP	RCACSEL

RCSPIN:
	EX	AF,AF'
	CP	2
	JP	C,ROT
	CP	5
	JP	C,RRR
	JR	Z,ROT
LLL:	LD	A,(DRIFDR)
	OR	A
	JP	Z,TATI
	JP	ROT
RRR:	LD	A,(DRIFDR)
	OR	A
	JP	Z,ROT
TATI:	XOR	A
	LD	(SPIN),A
	LD	(DRIF),A
	LD	A,(MH)
	LD	(MDR),A
	JP	RCACSEL
ROT:	LD	A,(DRIFDR)
	OR	A
	LD	A,SPINR
	JP	Z,ROTL1
	NEG
ROTL1:	LD	B,A
	LD	A,(MDR)
	ADD	A,B
	LD	(MDR),A
	LD	A,(MSPD)
	LD	B,SPINM
	SUB	B
	JP	C,TATI
	LD	(MSPD),A
	JP	RCBREAK

RCACSEL:
	LD	A,(ACFLAG)
	XOR	1
	LD	(ACFLAG),A
	JP	Z,RCBREAK
	XOR	A
	CALL	GTTRIG
	LD	B,A
	LD	A,1
	PUSH	BC
	CALL	GTTRIG
	POP	BC
	OR	B
	JP	Z,PREBREAK
	LD	A,(MSPD)
	LD	B,A
	SRL	A
	SRL	A
	SRL	A
	SRL	A
	LD	E,A
	LD	D,0
	LD	HL,SPDTAB
	ADD	HL,DE
	LD	A,(HL)
	ADD	A,B
	JP	NC,LSV
	LD	A,255
LSV:	LD	HL,MAXSPD
	CP	(HL)
	JP	C,LSV2
	LD	A,(HL)
LSV2:	LD	(MSPD),A
	JP	RCBREAK
PREBREAK:
	LD	A,(MSPD)
	SUB	IDOL
	JP	NC,PREL1
	XOR	A
PREL1:	LD	(MSPD),A

RCBREAK:
	LD	A,(ACFLAG)
	OR	A
	JP	Z,SOKUDO
	LD	A,6
	CALL	SNSMAT
	AND	04H
	XOR	04H
	LD	B,A
	LD	A,3
	PUSH	BC
	CALL	GTTRIG
	NEG
	POP	BC
	OR	B
	JP	Z,SOKUDO
	LD	A,(MSPD)
	SUB	VBREAK
	JP	NC,RBLAB
	LD	A,0
RBLAB:	LD	(MSPD),A
	JP	SOKUDO

GET_DEG:
	LD	A,(MH) ;
	ADD	A,8 ;
	AND	0F0H ;
	LD	E,A ;
	;LD	A,(MH)
	LD	A,E ;
	AND	3FH		
	LD	B,A
	;LD	A,(MH)
	LD	A,E
	AND	0C0H
	CP	01000000B
	JP	Z,GDN
	CP	11000000B
	JP	Z,GDN
	LD	A,B
	RET
GDN:	LD	A,B
	NEG
	ADD	A,64
	RET

SOKUDO:
	CALL	GET_DEG
	NEG
	ADD	A,64
	CALL	GET_COS
	LD	B,A	
	LD	A,(MSPD)
	CALL	MULT
	SLA	L
	RL	H
	LD	A,H
	LD	(XSPD),A

	CALL	GET_DEG
	CALL	GET_COS
	LD	B,A
	LD	A,(MSPD)
	CALL	MULT
	SLA	L
	RL	H
	LD	A,H
	LD	(YSPD),A

SOUKOU:	LD	H,0
	LD	A,(MH)
	AND	80H
	LD	L,A
	JP	NZ,SKNEG
	LD	A,(XSPD)
	LD	B,A
	LD	A,(XDIS)
	ADD	A,B
	LD	(XDIS),A
	JP	NC,SOU_Y
	JP	SOKL1
SKNEG:	LD	A,(XSPD)
	LD	B,A
	LD	A,(XDIS)
	SUB	B
	LD	(XDIS),A
	JP	NC,SOU_Y

SOKL1:	INC	H
	LD	A,(MX)
	LD	(MXOLD),A
	DEC	L
	INC	L
	JP	NZ,SKL1D
	INC	A
	JP	SKL2
SKL1D:	DEC	A
SKL2:	LD	(MX),A

SOU_Y:	LD	A,(MH)
	AND	0C0H
	OR	A
	JP	Z,YNEG
	CP	0C0H
	JP	Z,YNEG
YPOS:	LD	L,0
	LD	A,(YSPD)
	LD	B,A
	LD	A,(YDIS)
	ADD	A,B
	LD	(YDIS),A
	JP	NC,CHECK
	JP	YCHN
YNEG:	LD	L,1
	LD	A,(YSPD)
	LD	B,A
	LD	A,(YDIS)
	SUB	B
	LD	(YDIS),A
	JP	NC,CHECK

YCHN:	INC	H
	LD	A,(MY)
	LD	(MYOLD),A
	DEC	L
	JP	Z,SKL3
	INC	A
	JP	SKL4
SKL3:	DEC	A
SKL4:	LD	(MY),A

CHECK:	LD	A,H
	OR	A
	JP	Z,RIVAL
	LD	A,(MX)
	LD	B,A
	LD	A,(MY)
	CALL	GET_PARTS
	CP	ROAD
	JP	Z,RIVAL
	CP	KUSA
	JP	Z,PKUSA
	CP	SUNA
	JP	Z,PSUNA
	CP	WAT
	JP	Z,PWAT
	CP	SIKIRI
	JP	Z,PSIKIRI
	CP	CT_L
	JP	Z,PCON
	CP	CT_R
	JP	Z,PCON
	CP	CT_W
	JP	Z,PCON
	CP	CT_K
	JP	Z,PCON
	JP	RIVAL
PKUSA:	LD	HL,MSPD
	LD	A,KUSASP
	CP	(HL)
	JP	NC,RIVAL
	LD	(HL),A
	JP	RIVAL
PSUNA:	LD	HL,MSPD
	LD	A,SUNASP
	CP	(HL)
	JP	NC,RIVAL
	LD	(HL),A
	JP	RIVAL
PSIKIRI:
	LD	A,(MXOLD)
	LD	(MX),A
	LD	A,(MYOLD)
	LD	(MY),A
	XOR	A
	LD	(MSPD),A
	LD	(DRIF),A
	LD	(SPIN),A
	LD	A,(MDR)
	LD	(MH),A
	JP	RIVAL
PWAT:
	LD	A,(MSPD)
	SUB	WATRED
	LD	(MSPD),A
	CP	WDROP
	JP	NC,RIVAL
	XOR	A
	LD	(MSPD),A
	LD	(DRIF),A
	LD	(SPIN),A
	LD	A,WATSPD
	LD	(MSPD),A
	JP	RIVAL

PCON:	LD	A,(CONDR)
	CP	1
	JP	C,CONDU
	JP	Z,CONLR
	CP	3
	JP	C,CONUD
CONRL:	LD	A,(MXOLD)
	LD	B,A
	LD	A,(MX)
	SUB	B
	CP	-1
	JP	NZ,UDT1
	LD	A,(MX)
	DEC	A
	LD	(MX),A
	LD	(MXOLD),A
	JP	PCON2
UDT1:	OR	A
	JP	Z,RIVAL
	LD	A,1
	LD	(ELLIGAL),A
	LD	A,(MX)
	INC	A
	LD	(MX),A
	LD	(MXOLD),A
	JP	RIVAL
CONDU:
	LD	A,(MYOLD)
	LD	B,A
	LD	A,(MY)
	SUB	B
	CP	-1
	JP	NZ,UDT2
	LD	A,(MY)
	DEC	A
	LD	(MY),A
	LD	(MYOLD),A
	JP	PCON2
UDT2:	OR	A
	JP	Z,RIVAL
	LD	A,1
	LD	(ELLIGAL),A
	LD	A,(MY)
	INC	A
	LD	(MY),A
	LD	(MYOLD),A
	JP	RIVAL
CONLR:	LD	A,(MXOLD)
	LD	B,A
	LD	A,(MX)
	SUB	B
	CP	1
	JP	NZ,UDT3
	LD	A,(MX)
	INC	A
	LD	(MX),A
	LD	(MXOLD),A
	JP 	PCON2
UDT3:	OR	A
	JP	Z,RIVAL
	LD	A,1
	LD	(ELLIGAL),A
	LD	A,(MX)
	DEC	A
	LD	(MX),A
	LD	(MXOLD),A
	JP	RIVAL
CONUD:
	LD	A,(MYOLD)
	LD	B,A
	LD	A,(MY)
	SUB	B
	CP	1
	JP	NZ,UDT4
	LD	A,(MY)
	INC	A
	LD	(MY),A
	LD	(MYOLD),A
	JP	PCON2
UDT4:	OR	A
	JP	Z,RIVAL
	LD	A,1
	LD	(ELLIGAL),A
	LD	A,(MY)
	DEC	A
	LD	(MY),A
	LD	(MYOLD),A
	JP	RIVAL
	
PCON2:	LD	A,(ELLIGAL)
	OR	A
	JP	Z,PCON3
	XOR	A
	LD	(ELLIGAL),A
	JP	RIVAL

PCON3:	LD	A,(MLAP)
	INC	A
	JR	Z,PC3L1
	CALL	SVLAP
	CALL	PRLAP
PC3L1:	LD	A,(MLAP)
	INC	A
	LD	(MLAP),A
	CALL	MESS
;	LD	A,(MLAP)
;	CALL	POSITION	;POS CHECK
;	LD	(MPOS),A
;	CALL	TPRPOS
;	LD	A,(MPOS)
;	LD	B,A
;	LD	HL,5ADDH
;	CALL	PRFIG
	LD	A,(MLAP)
	LD	HL,LAP
	CP	(HL)
	JP	NZ,RIVAL
GOALS:	LD	A,1
	LD	(GOALF),A
	XOR	A
	LD	(SOUND),A
	XOR	A
	CALL	TIMER
	XOR	A
	CALL	LTIMER
	LD	A,(CONDR)
	RRCA
	RRCA
	LD	(MDR),A
	CALL	GOS
	RET
	;####GOAL SYORI

RIVAL:
	;LD	B,RBV

SCROLL:	LD	A,1
	LD	(SOUND),A
	LD	A,(MSPD)
	OR	A
	JP	NZ,SCRL1
	LD	(SOUND),A
;	LD	A,(POSF)
;	OR	A
;	JP	Z,SCRL1
;POSCK:	LD	A,(POSCNT)
;	OR	A
;	JP	NZ,SCRL1
;	LD	HL,5ADDH
;	LD	B,-1
;	CALL	PRFIG
;	XOR	A
;	LD	(POSF),A

SCRL1:	CALL	PRSPD
	LD	A,(MX)
	SUB	11
	LD	B,A
	LD	A,(MY)
	SUB	11
	LD	C,A
	CALL	PCOURSE
GWAIT:	LD	A,(POSCNT)
	OR	A
	JP	NZ,GWAIT
	JP	RACING

GOS:	LD	D,12
	LD	A,(MX)
	SUB	11
	LD	B,A
	LD	A,(MY)
	SUB	11
	LD	C,A
	LD	A,(CONDR)
	CP	1
	JP	C,GODU
	JP	Z,GOLR
	CP	3
	JP	C,GOUD
GORL:	
	LD	A,(MX)
	DEC	A
	LD	(MX),A
	PUSH	DE
	PUSH	BC
	CALL	PCOURSE
	LD	A,2
	CALL	WAIT
	POP	BC
	POP	DE
	DEC	D
	JP	NZ,GORL
	LD	A,60
	CALL	WAIT
	JP	GONEXT
GOLR:	
	LD	A,(MX)
	INC	A
	LD	(MX),A
	PUSH	DE
	PUSH	BC
	CALL	PCOURSE
	LD	A,2
	CALL	WAIT
	POP	BC
	POP	DE
	DEC	D
	JP	NZ,GOLR
	LD	A,60
	CALL	WAIT
	JP	GONEXT
GODU:	
	LD	A,(MY)
	DEC	A
	LD	(MY),A
	PUSH	DE
	PUSH	BC
	CALL	PCOURSE
	LD	A,2
	CALL	WAIT
	POP	BC
	POP	DE
	DEC	D
	JP	NZ,GODU
	LD	A,60
	CALL	WAIT
	JP	GONEXT
GOUD:	
	LD	A,(MY)
	INC	A
	LD	(MY),A
	PUSH	DE
	PUSH	BC
	CALL	PCOURSE
	LD	A,2
	CALL	WAIT
	POP	BC
	POP	DE
	DEC	D
	JP	NZ,GOUD
	LD	A,60
	CALL	WAIT
GONEXT:
	CALL	CLS_SUB
	LD	A,(SLCOURSE)
	ADD	A,'0'
	LD	(GOME1+10),A
	LD	HL,GOME1
	LD	DE,1800H+32*3+6
	LD	BC,11
	CALL	LDIRVM
	LD	HL,GOME2
	LD	DE,1800H+32*5+8
	LD	BC,6
	CALL	LDIRVM
	LD	HL,SVAREA
	LD	B,5
GONL1:	PUSH	BC
	LD	A,6
	SUB	B
	LD	C,A
	ADD	A,'0'
	LD	(GOME3+1),A
	LD	A,(HL)
	INC	HL
	ADD	A,'0'
	LD	(DGOME3+5),A
	LD	A,(HL)
	INC	HL
	ADD	A,'0'
	LD	(DGOME3+3),A
	LD	A,(HL)
	INC	HL
	ADD	A,'0'
	LD	(DGOME3+2),A
	LD	A,(HL)
	INC	HL
	ADD	A,'0'
	LD	(DGOME3),A
	PUSH	HL
	LD	A,C
	DEC	A
	SLA	A
	ADD	A,8
	LD	E,A
	LD	D,0
	SLA	E
	RL	D
	SLA	E
	RL	D
	SLA	E
	RL	D
	SLA	E
	RL	D
	SLA	E
	RL	D
	LD	HL,1800H+6
	ADD	HL,DE
	EX	DE,HL
	LD	HL,GOME3
	LD	BC,10
	CALL	LDIRVM
	LD	A,30
	CALL	WAIT
	POP	HL
	POP	BC
	DJNZ	GONL1
	LD	HL,GOME4
	LD	DE,1800H+32*18+4
	LD	BC,5
	CALL	LDIRVM
	LD	A,(T10SEC)
	ADD	A,'0'
	LD	(DGOME3+5),A
	LD	A,(T1SEC)
	ADD	A,'0'
	LD	(DGOME3+3),A
	LD	A,(TSEC)
	ADD	A,'0'
	LD	(DGOME3+2),A
	LD	A,(MINUTE)
	ADD	A,'0'
	LD	(DGOME3),A
	LD	HL,DGOME3
	LD	DE,1800H+32*18+10
	LD	BC,6
	CALL	LDIRVM
		
GONL2:	XOR	A
	CALL	GTTRIG
	PUSH	AF
	LD	A,1
	CALL	GTTRIG
	LD	B,A
	POP	AF
	OR	B
	JP	Z,GONL2
	CALL	CLS_SUB
	LD	A,60
	CALL	WAIT
	RET
	
GOME1:	DB	'COURSE',2FH,'No.',0
GOME2:	DB	'RESULT'
GOME3:	DB	'L',0,2FH,2FH
DGOME3:	DB	0,'.',0,0,'.',0
GOME4:	DB	'TOTAL'

CLS_SUB:LD	D,24
	LD	HL,1800H
CLSSL1:	LD	BC,23
	LD	A,2FH
	CALL	FILVRM
	LD	BC,32
	ADD	HL,BC
	DEC	D
	JP	NZ,CLSSL1
	LD	A,209
	LD	D,A
	LD	HL,1B00H
	CALL	WRTVRM
	LD	HL,1B04H
	LD	A,D
	CALL	WRTVRM
	LD	HL,1B40H
	LD	A,D
	CALL	WRTVRM
	LD	HL,1B44H
	LD	A,D
	CALL	WRTVRM
	RET	
	
MULT:	LD	HL,0
	LD	E,B
	LD	D,0
	LD	B,8
MULL1:	RRCA
	JP	NC,MULL2
	ADD	HL,DE
MULL2:	SLA	E
	RL	D
	DJNZ	MULL1
	RET

GET_COS:
	LD	HL,COSDAT
	LD	E,A
	LD	D,0
	ADD	HL,DE
	LD	A,(HL)
	RET

GET_PARTS:
	LD	C,A
	LD	H,0
	LD	L,C
	SLA	L
	RL	H
	SLA	L
	RL	H
	SLA	L
	RL	H
	SLA	L
	RL	H
	SLA	L
	RL	H
	LD	D,H
	LD	E,L
	SLA	L
	RL	H
	ADD	HL,DE
	LD	D,0
	LD	E,B
	ADD	HL,DE
	LD	DE,CSDATA
	ADD	HL,DE
	LD	A,(HL)
	RET

SVLAP:	
	LD	A,(MLAP)
	SLA	A
	SLA	A
	LD	E,A
	LD	D,0
	LD	HL,SVAREA
	ADD	HL,DE
	LD	DE,LT10SEC
	EX	DE,HL
	LD	BC,4
	LDIR
	LD	A,1
	CALL	LTIMER
	RET

SVAREA:	DB	0,0,0,0	;1
	DB	0,0,0,0	;2
	DB	0,0,0,0	;3
	DB	0,0,0,0 ;4
	DB	0,0,0,0	;5

;POSITION:
;	LD	HL,POSINLAP	
;	LD	E,A
;	LD	D,0
;	ADD	HL,DE
;	INC	(HL)
;	LD	A,(HL)
;	RET	
;POSINLAP:
;	DB	0,0,0,0,0,0,0,0

TIMER:
	LD	(TIMEF),A
	OR	A
	RET	Z
RESETT:	LD	HL,INTIME
	LD	(HL),0
	INC	HL
	LD	(HL),0
	INC	HL
	LD	(HL),0
	INC	HL
	LD	(HL),0
	INC	HL
	LD	(HL),0
	INC	HL
	LD	(HL),0
	RET	

LTIMER:
	LD	(LTIMEF),A
	OR	A
	RET	Z
	LD	HL,LT10SEC
	LD	(HL),0
	INC	HL
	LD	(HL),0
	INC	HL
	LD	(HL),0
	INC	HL
	LD	(HL),0
	INC	HL
	LD	(HL),0
	RET	

;TPRPOS:
;	LD	A,1
;	LD	(POSF),A
;	LD	A,240
;	LD	(POSCNT),A
;	RET

PRE:
	LD	A,(MX)
	SUB	11
	LD	H,A
	LD	A,(MY)
	SUB	11
	LD	L,A
	LD	D,20
	LD	A,(CONDR)
	CP	1
	JP	C,UD
	JP	Z,RL
	CP	3
	JP	C,DU	;HL -> XY, D-> KASUU

LR:	LD	A,H
	SUB	D	
	LD	B,A
	LD	C,L
	CALL	PRESCR
	DEC	D
	JR	NZ,LR
	JP	PRESIG
RL:	LD	A,H
	ADD	A,D
	LD	B,A
	LD	C,L
	CALL	PRESCR
	DEC	D
	JR	NZ,RL
	JP	PRESIG
UD:	LD	A,L
	SUB	D
	LD	C,A
	LD	B,H
	CALL	PRESCR
	DEC	D
	JR	NZ,UD
	JP	PRESIG
DU:	LD	A,L
	ADD	A,D
	LD	C,A
	LD	B,H
	CALL	PRESCR
	DEC	D
	JR	NZ,DU
	JP	PRESIG
PRESCR:	PUSH	HL
	PUSH	DE
	CALL	PCOURSE
	POP	DE
	POP	HL
	LD	A,5
	CALL	WAIT
	RET

PRESIG:	LD	B,H
	LD	C,L
	CALL	PRESCR
	CALL	SIGNAL
	RET

PCOURSE:
	LD	A,B
	CP	96
	JP	C,PCST
	LD	A,96
	ADD	A,B	;A=A-(-B)
	LD	B,A
PCST:	PUSH	BC
	LD	A,C
	OR	A
	JP	P,PPCN
	NEG		;HU NO TOKI
	LD	E,A
	LD	D,0
	LD	HL,0
	OR	A
	SBC	HL,DE
	JP	PPCN1
PPCN:	LD	H,0
	LD	L,C
PPCN1:	SLA	L
	RL	H
	SLA	L
	RL	H
	SLA	L
	RL	H
	SLA	L
	RL	H
	SLA	L
	RL	H
	LD	D,H
	LD	E,L
	SLA	L
	RL	H
	ADD	HL,DE
	LD	D,0
	LD	E,B
	ADD	HL,DE
	LD	DE,CSDATA
	ADD	HL,DE

	LD	A,(7)
	INC	A
	LD	C,A
	LD	DE,5800H
	LD	B,24
PCL1:	DI
	OUT	(C),E
	OUT	(C),D
	EI
	PUSH	BC
	DEC	C
	LD	B,23
PCL2:	LD	A,(HL)
	INC	HL
	OUT	(C),A
	DJNZ	PCL2
	LD	BC,73
	ADD	HL,BC
	LD	A,32
	ADD	A,E
	JR	NC,PCL3
	INC	D
PCL3:	LD	E,A
	POP	BC
	DJNZ	PCL1
	POP	BC
	CALL	PSPRITE	;SPRITE HYOUJI
	RET

PSPRITE:	
	LD	A,(MX)
	ADD	A,96
	SUB	B
	CP	96
	JP	C,ADL1
	SUB	96
ADL1:
	CP	22
	JR	NC,PSNON
	SLA	A
	SLA	A
	SLA	A
	LD	D,A

	LD	A,C
	OR	A
	JP	P,DUM1
	LD	A,(MY)
	SUB	C
	JP	DUM2
DUM1:	LD	A,(MY)
	SUB	C	
	JR	C,PSNON
DUM2:	CP	23
	JR	NC,PSNON
	SLA	A
	SLA	A
	SLA	A
	LD	E,A
	LD	A,(MDR)
	ADD	A,8
	AND	0F0H
	SRL	A
	LD	B,A
	JP	PSPL1
PSNON:	LD	E,209
PSPL1:	PUSH	DE	;XY HOZON
	LD	A,(7)
	INC	A
	LD	C,A
	XOR	A
	LD	H,5BH
	DI
	OUT	(C),A	;ADDRESS SET
	OUT	(C),H
	EI
	NOP
	DEC	C
	OUT	(C),E
	CALL	SPWT	;
	OUT	(C),D
	CALL	SPWT	;
	OUT	(C),B
	CALL	SPWT	;
	LD	A,(MCOL)
	OUT	(C),A
	CALL	SPWT	;
	OUT	(C),E
	CALL	SPWT	;
	OUT	(C),D
	CALL	SPWT	;
	LD	A,4
	ADD	A,B
	OUT	(C),A
	CALL	SPWT	;
	LD	A,1
	OUT	(C),A	;WINDOW COLOR
;!!!!!!!TEKISYA

SPMISC:	INC	C
	LD	L,64
	LD	H,5BH
	DI
	OUT	(C),L
	OUT	(C),H
	EI
	DEC	C
	POP	DE
	LD	A,(DRIF)
	OR	A
	JR	NZ,SML1
	LD	E,209
SML1:	OUT	(C),E
	CALL	SPWT	;
	OUT	(C),D
	CALL	SPWT	;
	LD	A,(DRIPTN)
	SLA	A
	SLA	A
	OUT	(C),A
	CALL	SPWT	;
	LD	A,15
	OUT	(C),A
SPMAP:	LD	HL,(MX)
	SRL	H
	SRL	L
	LD	A,16
	ADD	A,H
	DEC	A
	OUT	(C),A
	CALL	SPWT	;
	LD	A,200
	ADD	A,L
	DEC	A
	OUT	(C),A
	CALL	SPWT	;
	LD	A,34*4
	OUT	(C),A
	CALL	SPWT	;
	LD	A,SPMAPM
	OUT	(C),A
	RET

SPWT:	;INC	IX
	;DEC	IX
	RET

SIGNAL:	LD	A,60
	CALL	WAIT
	LD	A,97H
	LD	HL,1845H
	CALL	WRTVRM
	LD	A,89H
	LD	HL,1865H
	CALL	WRTVRM
	LD	A,89H
	LD	HL,1885H
	CALL	WRTVRM
	LD	A,89H
	LD	HL,18A5H
	CALL	WRTVRM
	CALL	SIG1
	LD	A,60
	CALL	WAIT
	LD	A,89H
	LD	HL,1845H
	CALL	WRTVRM
	LD	A,97H
	LD	HL,1865H
	CALL	WRTVRM
	CALL	SIG1
	LD	A,60
	CALL	WAIT
	LD	A,89H
	LD	HL,1865H
	CALL	WRTVRM
	LD	A,97H
	LD	HL,1885H
	CALL	WRTVRM
	CALL	SIG1
	LD	A,60
	CALL	WAIT
	LD	A,89H
	LD	HL,1885H
	CALL	WRTVRM
	LD	A,9FH
	LD	HL,18A5H
	CALL	WRTVRM
	CALL	SIG2
	LD	A,50
	CALL	WAIT
	RET	

SIG1:	LD	HL,SIGDT1
	JP	SIGSOUND
SIG2:	LD	HL,SIGDT2
SIGSOUND:
	LD	B,7
SSL1:	LD	A,(HL)
	INC	HL
	LD	E,(HL)
	INC	HL
	CALL	WRTPSG
	DJNZ	SSL1
	RET

SIGDT1:	DB	0,254,1,1,11,193,12,34,13,0,8,16,7,10111110B
SIGDT2:	DB	0,254,1,0,11,193,12,34,13,0,8,16,7,10111110B
		
MKSCR:	CALL	CLS2
	CALL	MAP
	LD	A,(SLCOURSE)
	CALL	PRMAP
;	CALL	MESS
	CALL	MKSCR_SUB
	CALL	PRSPD
	RET

PRMAP:
	OR	A
	JP	Z,MAPBLANK
	LD	HL,MAPADTBL
	DEC	A
	SLA	A
	LD	E,A
	LD	D,0
	ADD	HL,DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	LD	DE,0B8H*8
	LD	BC,120H
	CALL	LDIRVM
	RET
MAPBLANK:
	LD	HL,0B8H*8
	LD	BC,120H
	XOR	A
	CALL	FILVRM
	RET
MAPADTBL:
	DW	MAP1DT,MAP2DT,MAP3DT,MAP4DT,MAP5DT


PRSPD:	LD	A,(MSPD)
	LD	D,100
	CALL	PSL1
	LD	HL,5800H + 32*13 + 25
	EX	AF,AF'
	CALL	PRFIG
	EX	AF,AF'
	LD	D,10
	CALL	PSL1
	LD	HL,5800H + 32*13 + 27
	EX	AF,AF'
	CALL	PRFIG
	EX	AF,AF'
	LD	B,A
	LD	HL,5800H + 32*13 + 29
	CALL	PRFIG
	RET

PSL1:	LD	B,0
PSL2:	SUB	D
	JR	C,PSL3
	INC	B
	JR	PSL2
PSL3:	ADD 	A,D
	RET

PRFIG:	LD	A,(7)
	INC	A
	LD	C,A
	DI
	OUT	(C),L
	OUT	(C),H
	EI
	LD	A,B
	INC	A
	JP	Z,PRBLANK
	SLA	B
	SLA	B
	DEC	C
	OUT	(C),B
	INC	B
	INC	B
	OUT	(C),B
	LD	DE,32
	ADD	HL,DE
	INC	C
	DI
	OUT	(C),L
	OUT	(C),H
	EI
	DEC	C
	DEC	B
	OUT	(C),B
	INC	B
	INC	B
	OUT	(C),B
	RET

PRBLANK:
	LD	A,2FH
	OUT	(C),A
	OUT	(C),A
	LD	DE,32
	ADD	HL,DE
	INC	C
	DI
	OUT	(C),L
	OUT	(C),H
	EI
	DEC	C
	OUT	(C),A
	OUT	(C),A
	RET

MESS:	LD	A,(7)
	INC	A
	LD	C,A
	LD	A,(MLAP)
	LD	B,A
	LD	A,(LAP)
	SUB	B
	CP	1
	JR	C,MGOAL
	JR	Z,MFINAL
	ADD	A,'0'
	LD	HL,MS1
	JP	MESPR
MFINAL:	LD	HL,MS2
	LD	A,(HL)
	JP	MESPR
MGOAL:	LD	HL,MS3
	LD	A,(HL)
MESPR:	LD	DE,5800H + 32*9 + 24
	DI
	OUT	(C),E
	OUT	(C),D
	EI
	DEC	C
	OUT	(C),A
	INC	HL
	LD	B,4
MEL1:	LD	A,(HL)
	INC	HL
	OUT	(C),A
	DJNZ	MEL1
	INC	C
	LD	DE,5800H + 32*10 + 26
	DI
	OUT	(C),E
	OUT	(C),D
	EI
	DEC	C
	LD	B,5
MEL2:	LD	A,(HL)
	INC	HL
	OUT	(C),A
	DJNZ	MEL2
	RET
	
MS1:	DB	2FH,'LAPsTO',2FH,'GO'
MS2:	DB	'FINAL',2FH,'LAP',2FH
MS3:	DB	2FH,2FH,2FH,2FH,2FH,'GOAL',2FH

MAP:	LD	HL,WK1
	LD	DE,1800H + 32 + 24
	LD	BC,8
	CALL	LDIRVM

	LD	HL,1858H
	LD	D,0B8H
	LD	B,6
MPL1:	LD	A,28H
	CALL	WRTVRM
	INC	HL
	LD	C,6
MPL2:	LD	A,D
	CALL	WRTVRM
	INC	HL
	INC	D
	DEC	C
	JR	NZ,MPL2
	LD	A,28H
	CALL	WRTVRM
	LD	A,25
	ADD	A,L
	JR	NC,MPL3
	INC	H
MPL3:	LD	L,A
	DJNZ	MPL1

	LD	HL,WK2
	LD	DE,1800H + 32*8 +24
	LD	BC,8
	CALL	LDIRVM
	RET

WK1:	DB	2AH,29H,29H,29H,29H,29H,29H,2BH
WK2:	DB	2CH,29H,29H,29H,29H,29H,29H,2DH

MKSCR_SUB:
	LD	HL,WD1
	LD	DE,1800H + 32*12 + 24
	LD	BC,5
	CALL	LDIRVM
	LD	HL,WD2
	LD	DE,1800H + 32*16 + 24
	LD	BC,4
	CALL	LDIRVM
	LD	HL,WD3
	LD	DE,1800H + 32*19 + 24
	LD	BC,8
	CALL	LDIRVM
;	LD	HL,WD4
;	LD	DE,1800H + 32*22 + 24
;	LD	BC,4
;	CALL	LDIRVM
	LD	HL,WD5
	LD	DE,1800H + 32*17 + 25
	LD	BC,6
	CALL	LDIRVM
	LD	HL,WD5
	LD	DE,1800H + 32*20 + 25
	LD	BC,6
	CALL	LDIRVM
	RET

WD1:	DB	'SPEED'
WD2:	DB	'TIME'
WD3:	DB	'LAP',2FH,'TIME'
;WD4:	DB	'POS.'
WD5:	DB	'0.00.0'

PRMINUTE:
	LD	A,(7)
	INC	A
	LD	C,A
	LD	A,(MINUTE)
	ADD	A,'0'
	LD	HL,5A39H
	DI
	OUT	(C),L
	OUT	(C),H
	EI
	NOP
	DEC	C
	OUT	(C),A
	INC	C
PRTSEC:	LD	A,(TSEC)
	ADD	A,'0'
	LD	HL,5A3BH
	DI
	OUT	(C),L
	OUT	(C),H
	EI
	NOP
	DEC	C
	OUT	(C),A
	INC	C
PR1SEC:	LD	A,(T1SEC)
	ADD	A,'0'
	LD	HL,5A3CH
	DI
	OUT	(C),L
	OUT	(C),H
	EI
	NOP
	DEC	C
	OUT	(C),A
	INC	C
PR10SEC:
	LD	A,(T10SEC)
	ADD	A,'0'
	LD	HL,5A3EH
	DI
	OUT	(C),L
	OUT	(C),H
	EI
	NOP
	DEC	C
	OUT	(C),A
	RET	

PRLAP:
	LD	A,(MLAP)
	SLA	A
	SLA	A
	ADD	A,3
	LD	E,A
	LD	D,0
	LD	HL,SVAREA
	ADD	HL,DE
	EX	DE,HL
	LD	A,(7)
	INC	A
	LD	C,A
	LD	A,(DE)
	DEC	DE
	ADD	A,'0'
	LD	HL,5A99H
	DI
	OUT	(C),L
	OUT	(C),H
	EI
	NOP
	DEC	C
	OUT	(C),A
	INC	C
	LD	A,(DE)
	DEC	DE
	ADD	A,'0'
	LD	HL,5A9BH
	DI
	OUT	(C),L
	OUT	(C),H
	EI
	NOP
	DEC	C
	OUT	(C),A
	INC	C
	LD	A,(DE)
	DEC	DE
	ADD	A,'0'
	LD	HL,5A9CH
	DI
	OUT	(C),L
	OUT	(C),H
	EI
	NOP
	DEC	C
	OUT	(C),A
	INC	C
	LD	A,(DE)
	ADD	A,'0'
	LD	HL,5A9EH
	DI
	OUT	(C),L
	OUT	(C),H
	EI
	NOP
	DEC	C
	OUT	(C),A
	RET	

CLS2:	LD	HL,1800H
	LD	BC,32 * 24
	LD	A,2FH
	CALL	FILVRM
	LD	A,209
	LD	D,A
	LD	HL,1B00H
	CALL	WRTVRM
	LD	A,D
	LD	HL,1B04H
	CALL	WRTVRM
	LD	HL,1B40H
	LD	A,D
	CALL	WRTVRM
	LD	HL,1B44H
	LD	A,D
	CALL	WRTVRM
	RET

WAIT:	PUSH	HL
	LD	HL,T_CNT
	LD	(HL),0
WTL1:	CP	(HL)
	JR	NZ,WTL1
	POP	HL
	RET

INTON:	DI
	LD	HL,H.TIMI
	LD	DE,HK_SV
	LD	BC,5
	LDIR
	LD	A,0C3H
	LD	(H.TIMI),A
	LD	HL,TINT
	LD	(H.TIMI+1),HL
	EI
	RET

TINT:	DI
	LD	HL,T_CNT
	INC	(HL)
	INC	HL
	INC	(HL)	;INCNT++
	LD	A,(HL)
	AND	07H
	OR	A
	JP	NZ,ITIME ;	
	LD	HL,DRIPTN	;DRIFT	PT CHANGE
	LD	A,(HL)
	XOR	1
	LD	(HL),A

ITIME:
	LD	HL,INTIME 
	INC	(HL)
	LD	A,6
	CP	(HL)
	JP	NZ,LTINEXT
	LD	(HL),0
	LD	A,(CNTST)
	OR	A
	JP	NZ,TINEXT
	LD	A,(TIMEF)
	OR	A
	JP	Z,TINEXT
	
	INC	HL	;1/10 SEC
	INC	(HL)
	LD	A,10
	CP	(HL)
	JP	NZ,TINEXT
	LD	(HL),0
	INC	HL	;1SEC
	INC	(HL)
	CP	(HL)
	JP	NZ,TINEXT
	LD	(HL),0
	INC	HL	;TSEC
	INC	(HL)
	LD	A,6
	CP	(HL)
	JP	NZ,TINEXT
	LD	(HL),0
	INC	HL	;MINUTE
	INC	(HL)
	LD	A,10
	CP	(HL)
	JP	NZ,TINEXT
	LD	HL,T10SEC
	LD	(HL),9
	INC	HL
	LD	(HL),9
	INC	HL
	LD	(HL),5
	INC	HL
	LD	(HL),9
	INC	HL
	INC	(HL)	;CNTST
TINEXT:
LITIME:	LD	A,(LCNTST)
	OR	A
	JP	NZ,LTINEXT
	LD	A,(LTIMEF)
	OR	A
	JP	Z,LTINEXT

	LD	HL,LT10SEC	;1/10 SEC
	INC	(HL)
	LD	A,10
	CP	(HL)
	JP	NZ,LTINEXT
	LD	(HL),0
	INC	HL	;1SEC
	INC	(HL)
	CP	(HL)
	JP	NZ,LTINEXT
	LD	(HL),0
	INC	HL	;TSEC
	INC	(HL)
	LD	A,6
	CP	(HL)
	JP	NZ,LTINEXT
	LD	(HL),0
	INC	HL	;MINUTE
	INC	(HL)
	LD	A,10
	CP	(HL)
	JP	NZ,LTINEXT
	LD	HL,LT10SEC
	LD	(HL),9
	INC	HL
	LD	(HL),9
	INC	HL
	LD	(HL),5
	INC	HL
	LD	(HL),9
	INC	HL
	INC	(HL)
	JP	LTINEXT
LTINEXT:
	LD	A,(POSCNT)
	OR	A
	JP	Z,TIPON
	DEC	A
	LD	(POSCNT),A
TIPON:
	CALL	NORSOUND

TTL1:	EI
	JP	HK_SV

NORSOUND:
	LD	A,(SOUND)
	OR	A
	JP 	NZ,SNDL1
	LD	A,(NORSIN)
	OR	A
	RET	Z
	XOR	A
	LD	(NORSIN),A
	LD	E,10111111B
	LD	A,7
	CALL	WRTPSG
	LD	E,0
	LD	A,8
	CALL	WRTPSG
	RET
SNDL1:	LD	A,1
	LD	(NORSIN),A
	LD	A,(DRIF)
	OR	A
	JR	NZ,DRSOUND
	LD	A,(MSPD)
	CPL
	LD	B,A
	SLA	A
	LD	E,A
	XOR	A
	CALL	WRTPSG
	LD	A,B
	RLCA
	AND	1
	ADD	A,5
	LD	E,A
	LD	A,1
	CALL	WRTPSG
	LD	E,10111110B
	LD	A,7
	CALL	WRTPSG
	LD	E,12	;VOLUME
	LD	A,8
	CALL	WRTPSG
	RET
DRSOUND:
	LD	B,6
	LD	HL,DRSDATA
DRSDL1:	LD	A,(HL)
	INC	HL
	LD	E,(HL)
	INC	HL
	CALL	WRTPSG
	DJNZ	DRSDL1
	RET
DRSDATA:
	DB	6,26,7,10110111B,8,16,11,49,12,0,13,8

NORSIN:	DB	0
	
INTIME:	DB 	0
T10SEC:	DB	0
T1SEC:	DB	0
TSEC:	DB	0
MINUTE:	DB	0
CNTST:	DB	0
TIMEF:	DB	0

;LINTIME:	DB 	0
LT10SEC:	DB	0
LT1SEC:		DB	0
LTSEC:		DB	0
LMINUTE:	DB	0
LCNTST:		DB	0
LTIMEF:		DB	0

POSF:	DB	0
POSCNT:	DB	0

REV:	LD	A,(EXPTBL)
	LD	HL,4000H
	CALL	ENASLT
	DI
	LD	HL,HK_SV
	LD	DE,H.TIMI
	LD	BC,5
	LDIR
	EI
	RET

HK_SV:	DS	5

;######################
CSPARA:
	;X,Y,DR,SIZE
	DB	10,47,0
	DW	5D9H
	DB	45,81,1
	DW	7F7H
	DB	25,13,1
	DW	973H
	DB	13,90,1
	DW	59DH
	DB	22,39,1
	DW	61BH

WORKAREA:

MAXSPD:	DB	0
LAP:	DB	0
CONDR:	DB	0

MYCAR:

MX:	DB	0
MY:	DB	0
MCOL:	DB	8
MSPD:	DB	0
MLAP:	DB	0
MH:	DB	0
MDR:	DB	0
DRIF:	DB	0
SPIN:	DB	0
DRIFDR:	DB	0
GOALF:	DB	0
XSPD:	DB	0
YSPD:	DB	0
XDIS:	DB	0
YDIS:	DB	0
MXOLD:	DB	0
MYOLD:	DB	0
;MPOS:	DB	0
DRIPTN:	DB	20H
DMH:	DB	VHAN
DMDF:	DB	VDRIF
DSPIN:	DB	VSPIN

T_CNT:	DB	0
INCNT:	DB	0

SLCOURSE:
	DB	0
SOUND:	DB	0
ACFLAG:	DB	0
HANFLAG:
	DB	1
ELLIGAL:
	DB	0

SPDTAB:	DB	7,7,6,6,6,5,5,4
	DB	4,4,3,3,3,2,2,1
COSDAT:
	DB	07FH
	DB	07EH,07EH,07EH,07EH,07EH,07DH,07DH,07CH
	DB	07BH,07BH,07AH,079H,078H,077H,076H,075H
	DB	074H,072H,071H,070H,06EH,06CH,06BH,069H
	DB	067H,066H,064H,062H,060H,05EH,05BH,059H
	DB	057H,055H,052H,050H,04EH,04BH,049H,046H
	DB	043H,041H,03EH,03BH,039H,036H,033H,030H
	DB	02DH,02AH,027H,024H,021H,01EH,01BH,018H
	DB	015H,012H,0FH,0CH,09H,06H,03H,00H	
MAP1DT:
	DB	00H,00H,00H,01FH,01FH,01FH,01FH,01FH,00H,00H,00H,0FFH,0FFH,0FFH,0FFH,0FFH
	DB	00H,00H,00H,0FFH,0FFH,0FFH,0FFH,0FFH,00H,00H,00H,0FFH,0FFH,0FFH,0FFH,0FFH
	DB	00H,00H,00H,0FCH,0FFH,0FFH,0FFH,0FFH,00H,00H,00H,00H,00H,0C0H,0C0H,0E0H
	DB	01FH,01FH,01FH,01FH,01FH,01FH,01FH,01FH,0C0H,080H,00H,00H,01H,03H,07H,07H
	DB	00H,00H,00H,00H,0FFH,0FFH,0FFH,0FFH,00H,00H,00H,00H,0FFH,0FFH,0FFH,0FFH
	DB	01FH,07H,07H,07H,0FFH,0FFH,0FFH,0FFH,0F0H,0F0H,0F0H,0F0H,0F0H,0E0H,0C0H,0C0H
	DB	01FH,01FH,01FH,01FH,01FH,01FH,01FH,01FH,0FH,0FH,07H,07H,03H,01H,00H,00H
	DB	0FFH,0FFH,0FCH,0FFH,0FFH,0FFH,07FH,01FH,0FFH,0FFH,00H,080H,0E0H,0F8H,0FEH,0FFH
	DB	0FFH,0FCH,00H,00H,00H,00H,00H,0C0H,00H,00H,00H,00H,00H,00H,00H,00H
	DB	01FH,01FH,01FH,01FH,01FH,01FH,01FH,01FH,00H,00H,00H,00H,00H,00H,00H,00H
	DB	07H,01H,00H,00H,00H,00H,00H,0F0H,0FFH,0FFH,07FH,01FH,07H,00H,00H,00H
	DB	0F0H,0FCH,0FFH,0FFH,0FFH,0FFH,03FH,0FH,00H,00H,00H,0C0H,0E0H,0F0H,0F0H,0F8H
	DB	01FH,01FH,01FH,01FH,01FH,01FH,01FH,01FH,00H,01H,03H,03H,07H,07H,0FH,09FH
	DB	0FFH,0FFH,0FFH,0FFH,0FFH,0E1H,0C0H,080H,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,00H,00H
	DB	0C7H,0FFH,0FFH,0FFH,0FFH,0FFH,01EH,00H,0F8H,0F8H,0F0H,0F0H,0E0H,0C0H,00H,00H
	DB	01FH,01FH,01FH,01FH,01FH,01FH,00H,00H,0FFH,0FFH,0FEH,0FCH,0FCH,0F0H,00H,00H
	DB	080H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H
	DB	00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H

MAP2DT:
	DB	00H,00H,01FH,01FH,01FH,01FH,01FH,01FH,00H,00H,0E0H,0FFH,0FFH,0FFH,0FFH,0FFH
	DB	00H,00H,00H,0C0H,0F0H,0F8H,0F8H,0FCH,00H,00H,00H,0FH,0FH,01FH,01FH,01FH
	DB	00H,00H,00H,0FFH,0FFH,0FFH,0FFH,0FFH,00H,00H,00H,0F0H,0F8H,0FCH,0FCH,0FCH
	DB	01FH,01FH,0FH,0FH,07H,07H,03H,01H,0FFH,0FFH,0E7H,0F3H,0F1H,0F8H,0FCH,0FEH
	DB	0FEH,0FEH,0FEH,0FEH,0FEH,0FFH,0FFH,07FH,01FH,03FH,01FH,01FH,0FH,0FH,07H,07H
	DB	0FFH,0FFH,0FFH,0FFH,0F9H,0FCH,0FCH,0FEH,0FCH,0FCH,0FCH,0FCH,0FCH,0FCH,0FCH,07CH
	DB	00H,00H,00H,00H,00H,00H,00H,00H,0FEH,07FH,03FH,01FH,03FH,03FH,03FH,07FH
	DB	03FH,03FH,03FH,0BFH,0BFH,0BFH,09FH,09FH,03H,03H,01H,01H,00H,00H,080H,080H
	DB	0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,07FH,07CH,07CH,0FCH,0F8H,0F8H,078H,078H,078H
	DB	00H,00H,01H,03H,07H,0FH,0FH,01FH,07FH,0FFH,0FFH,0FFH,0FEH,0FCH,0F8H,0E0H
	DB	09FH,08FH,07H,03H,01H,01H,00H,00H,0C0H,0C0H,0F0H,0F8H,0FEH,0FFH,0FFH,03FH
	DB	07FH,07FH,03FH,07FH,0FFH,0FEH,0FEH,0FCH,078H,078H,078H,078H,0F8H,0F8H,0F8H,0F8H
	DB	01FH,03FH,03FH,03FH,03FH,01FH,01FH,01FH,0C0H,0C0H,0C0H,0C1H,0EFH,0FFH,0FFH,0FFH
	DB	00H,00H,00H,080H,0FEH,0FFH,0FFH,0FFH,01FH,07H,00H,00H,00H,0E0H,0F7H,0FFH
	DB	0FDH,0F3H,047H,0FH,03FH,0FFH,0FFH,0FFH,0F8H,0F0H,0F0H,0F0H,0F0H,0E0H,0E0H,0C0H
	DB	0FH,0FH,07H,03H,00H,00H,00H,00H,0FFH,0FFH,0FCH,0C0H,00H,00H,00H,00H
	DB	0FFH,0FFH,03FH,0FH,03H,00H,00H,00H,0FFH,0FFH,0FFH,0FFH,0FFH,00H,00H,00H
	DB	0FFH,0FEH,0F8H,0F0H,0C0H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H

MAP3DT:
	DB	00H,00H,00H,00H,00H,00H,01H,03H,00H,00H,00H,00H,07H,07FH,0FFH,0FFH
	DB	00H,00H,00H,01FH,0FFH,0FFH,0FFH,0FFH,00H,00H,00H,0C0H,0F0H,0F8H,0FCH,0FCH
	DB	00H,00H,0FH,03FH,07FH,07FH,07FH,07FH,00H,00H,00H,0D0H,0F8H,0F8H,0FCH,0FCH
	DB	07H,0FH,01FH,03FH,07FH,07FH,07EH,07EH,0FFH,0FFH,0E0H,080H,00H,00H,00H,03EH
	DB	0FFH,00H,00H,00H,00H,00H,00H,00H,0FEH,03EH,03FH,01FH,0FH,0FH,07H,03H
	DB	07EH,03EH,01FH,01FH,0BFH,0FFH,0FFH,0FFH,0FCH,07EH,03EH,03EH,03EH,03EH,0BEH,03EH
	DB	07EH,07CH,07FH,07FH,07FH,07FH,07FH,07FH,07FH,0FFH,0FFH,0FFH,0FFH,0FFH,0C7H,083H
	DB	0C0H,0F8H,0F8H,0F8H,0FCH,0FCH,0F9H,0F3H,01H,00H,03CH,07EH,0FFH,0FFH,0FFH,0FFH
	DB	0FFH,0FEH,07CH,00H,080H,0C0H,0F0H,0F0H,03EH,03EH,03EH,03EH,03EH,07EH,07EH,07EH
	DB	07FH,07FH,07EH,03CH,00H,00H,00H,01H,083H,03H,07H,0FH,01FH,07FH,0FEH,0FCH
	DB	0E3H,0E7H,0C7H,08FH,08FH,01FH,03FH,07EH,0FFH,0FFH,0CFH,09FH,09FH,03FH,03EH,07EH
	DB	0F0H,0E0H,0C0H,080H,080H,00H,00H,00H,07EH,07EH,07CH,07CH,07CH,0FCH,0FCH,0FCH
	DB	07H,0FH,01FH,03FH,03FH,03FH,03FH,03FH,0F9H,0E3H,0E3H,0C7H,09FH,0BFH,0FFH,0FFH
	DB	0FEH,0FEH,0FCH,0FCH,0F8H,0F8H,0F0H,0C0H,07EH,07CH,07CH,0FCH,0FCH,0FCH,0FCH,0FFH
	DB	00H,00H,01H,01H,03H,03H,07H,0FFH,0FCH,0F8H,0F8H,0F8H,0F8H,0F8H,0F8H,0F8H
	DB	03FH,03FH,01FH,0FH,00H,00H,00H,00H,0FFH,0FFH,0FFH,0FCH,0F0H,00H,00H,00H
	DB	080H,00H,00H,00H,00H,00H,00H,00H,0FFH,0FFH,07FH,07FH,03FH,01FH,00H,00H
	DB	0FFH,0FFH,0FFH,0FFH,0FFH,0E0H,00H,00H,0F0H,0E0H,0C0H,0C0H,00H,00H,00H,00H
		
MAP4DT:
	DB	00H,0FH,03FH,07FH,07FH,07FH,07EH,07EH,00H,0FFH,0FFH,0FFH,0FFH,0FFH,00H,00H
	DB	00H,0FFH,0FFH,0FFH,0FFH,00H,00H,00H,00H,0FFH,0FFH,0FFH,0FFH,03H,00H,00H
	DB	00H,0F8H,0FFH,0FFH,0FFH,0FFH,0FH,07H,00H,00H,00H,0E0H,0FCH,0FFH,0FFH,0FEH
	DB	07EH,07FH,07FH,07FH,03FH,03FH,01FH,01FH,00H,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH
	DB	00H,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0EFH,00H,0F0H,0FCH,0FFH,0FFH,0FFH,0FFH,0FFH
	DB	07H,03H,03H,03H,083H,0C1H,0E1H,0F1H,0F8H,0F0H,0E0H,0E1H,0C3H,087H,087H,0FH
	DB	07H,01H,00H,00H,00H,00H,00H,00H,0FEH,0FCH,00H,00H,00H,00H,00H,00H
	DB	01H,00H,00H,00H,00H,00H,00H,00H,0FFH,07FH,01FH,0FH,01H,00H,00H,00H
	DB	0F9H,0FCH,0FCH,0F0H,0C0H,00H,00H,00H,01FH,01FH,03FH,07FH,0FFH,0BFH,03FH,07FH
	DB	00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H,00H
	DB	00H,00H,00H,00H,00H,00H,01H,07H,00H,00H,00H,03H,01FH,07FH,0FFH,0FFH
	DB	00H,00H,0FCH,0FCH,0F1H,0F1H,0E1H,0E1H,05DH,03FH,037H,01DH,0FH,04H,081H,0C1H
	DB	00H,00H,00H,00H,03H,01FH,03FH,03FH,0FH,01FH,07FH,0FFH,0FFH,0FFH,0FFH,0FCH
	DB	0FFH,0FFH,0FFH,0FFH,0FFH,0F0H,080H,00H,0FFH,0FFH,0FEH,0F8H,080H,00H,00H,00H
	DB	081H,03H,03H,03H,07H,07H,0FH,01FH,0F0H,0F8H,0FFH,0FFH,0FFH,0FFH,0FEH,0FCH
	DB	07FH,07FH,07EH,07FH,07FH,07FH,07FH,07FH,0F0H,0C0H,00H,0FFH,0FFH,0FFH,0FFH,0FFH
	DB	00H,00H,00H,0FFH,0FFH,0FFH,0FFH,0FFH,00H,00H,01H,0FFH,0FFH,0FFH,0FFH,0FFH
	DB	03FH,07FH,0FFH,0FFH,0FFH,0FEH,0FCH,0F0H,0F8H,0F0H,0E0H,0C0H,080H,00H,00H,00H
	
MAP5DT:
	DB	00H,00H,00H,00H,01H,03H,03H,07H,00H,00H,00H,00H,0C0H,0E0H,0E0H,0F0H
	DB	00H,00H,00H,00H,07FH,07FH,07FH,07FH,00H,00H,00H,00H,0FFH,0FFH,0FFH,0FFH
	DB	00H,00H,00H,07FH,0FFH,0FFH,0FFH,0FFH,00H,00H,060H,0FEH,0FEH,0FEH,0FEH,0FEH
	DB	07H,07H,0FH,0FH,0FH,0FH,01FH,01FH,0F0H,0F0H,0F8H,0F0H,0E0H,0C0H,080H,080H
	DB	07FH,0FFH,0FFH,00H,00H,00H,00H,00H,0FFH,0FFH,0FFH,0FFH,00H,00H,00H,00H
	DB	0FFH,0FFH,0FCH,0F0H,00H,00H,00H,00H,0FEH,03EH,07EH,07EH,07CH,0FCH,0FCH,0F8H
	DB	01FH,03FH,03FH,03FH,01FH,00H,00H,00H,00H,0F8H,0FFH,0FFH,0FFH,0FFH,0FH,00H
	DB	00H,00H,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,00H,07FH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH
	DB	01H,0C1H,0C3H,0C3H,0C3H,087H,087H,083H,0F8H,0F0H,0E0H,0C0H,0C0H,080H,080H,080H
	DB	00H,00H,01FH,01FH,03FH,03FH,03FH,03FH,00H,00H,00H,0F8H,0FFH,0FFH,0FFH,0FFH
	DB	00H,00H,00H,00H,081H,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FEH
	DB	083H,083H,083H,083H,081H,081H,081H,01H,080H,080H,0C0H,0E0H,0E0H,0F0H,0F8H,0F8H
	DB	03FH,03FH,01FH,0FH,0FH,07H,07H,03H,01FH,00H,080H,080H,0C0H,0E0H,0E1H,0F7H
	DB	0FFH,00H,00H,00H,00H,07FH,0FFH,0FFH,00H,00H,00H,00H,0FFH,0FFH,0FFH,0FFH
	DB	01H,00H,00H,00H,080H,0C0H,0E0H,0F0H,0FCH,0FCH,0FEH,0FEH,0FEH,0FEH,0FEH,0FEH
	DB	01H,00H,00H,00H,00H,00H,00H,00H,0FFH,0FFH,07FH,07FH,03FH,01FH,00H,00H
	DB	0FFH,0FEH,0F8H,0F0H,0C0H,00H,00H,00H,0FFH,0FH,0FH,07H,03H,00H,00H,00H
	DB	0FCH,0FFH,0FFH,0FFH,0FFH,07FH,0EH,00H,0FEH,0FEH,0FEH,0FEH,0F8H,0E0H,00H,00H
	
	END	START

